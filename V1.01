//2023 DavidSauve4
int numSegments = 8;
float anglePerSegment;
float rotationSpeed = 0;
float currentAngle = 0;
boolean spinning = false;

void setup() {
  size(800, 800);
  anglePerSegment = 360.0 / numSegments;
}

void draw() {
  background(0);
  translate(width / 2, height / 2);
  currentAngle += rotationSpeed;
  
  if (spinning) {
    rotationSpeed = lerp(rotationSpeed, 0, 0.01);
    if (rotationSpeed < 0.01) {
      rotationSpeed = 0;
      spinning = false;
    }
  }
  
  for (int i = 0; i < numSegments; i++) {
    pushMatrix();
    rotate(radians(currentAngle + anglePerSegment * i));
    fill(i % 2 == 0 ? color(150, 0, 10) : color(0, 150, 10));
    arc(0, 0, width * 0.8, height * 0.8, 0, radians(anglePerSegment));
    popMatrix();
  }
  
  stroke(0);
  strokeWeight(3);
  line(0, 0, width * 0.4, 0);
}

void mousePressed() {
  if (!spinning) {
    spinning = true;
    rotationSpeed = random(5, 10);
  }
}
